buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 28
        compileSdkVersion = 35
        targetSdkVersion = 35
        ndkVersion = "26.1.10909125"
        kotlinVersion = "1.9.22"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.6.0")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
        classpath("com.google.firebase:firebase-crashlytics-gradle:2.9.9")
        classpath 'com.google.gms:google-services:4.4.2'                //firebase
    }
}

allprojects {
    configurations.all {
        resolutionStrategy {
            force 'androidx.browser:browser:1.8.0'
        }
    }
}

subprojects { subproject ->
    subproject.afterEvaluate {
        def android = subproject.extensions.findByName('android')
        if (android != null && android.defaultConfig != null) {
            def externalNativeBuild = android.defaultConfig.externalNativeBuild
            if (externalNativeBuild != null && externalNativeBuild.cmake != null) {
                def args = externalNativeBuild.cmake.arguments
                if (args != null && !args.any { it?.toString()?.startsWith('-DCMAKE_SHARED_LINKER_FLAGS=') }) {
                    args.addAll([
                        '-DCMAKE_SHARED_LINKER_FLAGS=-Wl,-z,max-page-size=16384',
                        '-DCMAKE_MODULE_LINKER_FLAGS=-Wl,-z,max-page-size=16384',
                        '-DCMAKE_EXE_LINKER_FLAGS=-Wl,-z,max-page-size=16384'
                    ])
                }
            }
        }
    }
}


apply plugin: "com.facebook.react.rootproject"
